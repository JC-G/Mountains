#extension GL_ARB_shader_atomic_counter_ops : enable
layout(binding=1) buffer Grid
{
	float GridData[];
};
layout(binding = 2) uniform atomic_uint marchableCount;
layout(binding = 3) uniform atomic_uint pointCount;
layout(std430,binding = 4) buffer X	{uvec4 marchableList[];}; //format is x,y,z,gridtype

layout(local_size_x=8,local_size_y=8,local_size_z=8) in;

const int totalTable[256] = {0,3,3,6,3,6,6,9,3,6,6,9,6,9,9,6,3,6,6,9,6,9,9,12,6,9,9,12,9,12,12,9,3,6,6,9,6,9,9,12,6,9,9,12,9,12,12,9,6,9,9,6,9,12,12,9,9,12,12,9,12,15,15,6,3,6,6,9,6,9,9,12,6,9,9,12,9,12,12,9,6,9,9,12,9,12,12,15,9,12,12,15,12,15,15,12,6,9,9,12,9,12,6,9,9,12,12,15,12,15,9,6,9,12,12,9,12,15,9,6,12,15,15,12,15,6,12,3,3,6,6,9,6,9,9,12,6,9,9,12,9,12,12,9,6,9,9,12,9,12,12,15,9,6,12,9,12,9,15,6,6,9,9,12,9,12,12,15,9,12,12,15,12,15,15,12,9,12,12,9,12,15,15,12,12,9,15,6,15,12,6,3,6,9,9,12,9,12,12,15,9,12,12,15,6,9,9,6,9,12,12,15,12,15,15,6,12,9,15,12,9,6,12,3,9,12,12,15,12,15,9,12,12,15,15,6,9,12,6,3,6,9,9,6,9,12,6,3,9,6,12,3,6,3,3,0};



void main()
{
	
	uvec3 gid = gl_GlobalInvocationID;
	if (gid.x >= gSize || gid.y >= gSize || gid.z >= gSize)
	{
		return;
	}
	float gridCells[8];
	uvec3 gridPos[8];

	gridPos[0] = gid;
	gridPos[1] = gid+uvec3(1,0,0);
	gridPos[2] = gid+uvec3(1,1,0);
	gridPos[3] = gid+uvec3(0,1,0);
	gridPos[4] = gid+uvec3(0,0,1);
	gridPos[5] = gid+uvec3(1,0,1);
	gridPos[6] = gid+uvec3(1,1,1);
	gridPos[7] = gid+uvec3(0,1,1);
	
	
	for (int i = 0; i < 8; i++)
	{
		gridCells[i] = GridData[getArrID(gridPos[i])];
	}


	int cubeIndex = 0;

	if (gridCells[0] > 0) cubeIndex |= 1;
	if (gridCells[1] > 0) cubeIndex |= 2;
	if (gridCells[2] > 0) cubeIndex |= 4;
	if (gridCells[3] > 0) cubeIndex |= 8;
	if (gridCells[4] > 0) cubeIndex |= 16;
	if (gridCells[5] > 0) cubeIndex |= 32;
	if (gridCells[6] > 0) cubeIndex |= 64;
	if (gridCells[7] > 0) cubeIndex |= 128;
	
	
	if (cubeIndex != 0 && cubeIndex != 255)
	{
		atomicCounterAddARB(pointCount,totalTable[cubeIndex]);
		uint bufferIndex = atomicCounterIncrement(marchableCount);
		uvec4 mc = uvec4(gid.x,gid.y,gid.z,cubeIndex);
		marchableList[bufferIndex] = mc;
	}
	
}

